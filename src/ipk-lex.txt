!=============================================================================== =
!This is the file that hosts the morphology for the Inupiaq morphological parser =
!Rules for phonology and orthography are kept in the rule-file xfst-ipk.txt      =
!=============================================================================== =

Multichar_Symbols
+N +V
+Sg +Du +Pl
+1Sg +2Sg +3Sg +4Sg +1Du +2Du +3Du +4Du +1Pl +2Pl +3Pl +4Pl
+1SgO +2SgO +3SgO +4SgO +1DuO +2DuO +3DuO +4DuO +1PlO +2PlO+3PlO +4PlO !Objects' persons
+Abs +Rel +Trm +Loc +Abl !cases
+Ind +Cau +InfReal +InfUnreal !Voiced is unrealized and voiceless is for ongoing or realized actions.


ch sr kh qh ł2 !digraphs plus ł2 for voiceless palatalized l
%> !morphemeborder
%TRUNC !truncation dummy
%CVCTRUNC !dummy for very long truncations
%VCTRUNC !dummy for long truncation
%FRIC !dummy for fricativizing stem-final consonants. Needed to avoid a general rule that also would affect unwantedly as in *aaġagu for aaqagu. The alternative would have been to postulate truncating flexives with a fricative first consonant (*aiviq -q +ġit) but that is hokus pokus
%C ! dummy for intermediate gemination
!%CLIT !dummy with clitics used in Kalaallisut. This far unused in ipk



!************************************
!Derivational morphemes and clitics *
!************************************

+NIAQ +NIAŊIT +SAAĠE ! verb elaborating
+VIK ! nominalizers
+LU +GUUQ ! clitics


LEXICON Root
	Nouns ;
	prop ;
	pron ;
	Verbs ;
!About lexica and continuations. Instead of separate lexica for words that can only be sing or only plur and others for words that can take all numbers, this is a better solution: Normal nouns are tagged tp, tup etc. whereas specials are tagged with the continuation lexicon

LEXICON tp !aġnaq ere
	tp_sing ;
	tp_du ;
	tp_pl ;

LEXICON tp_sing !erre
	ObliqueCases_Sg ;
+N+Abs+Sg:	clit ;
+N+Rel+Sg:%>%TRUNCm	clit ;

LEXICON tp_du
+N+Abs+Du:%C%>k	clit ;
+N:%C%>k	ObliqueCases_Du ; 


LEXICON tp_pl
+N+Abs+Pl:%TRUNC%>t	clit ;
+N:%TRUNC%>t	ObliqueCases_Pl ; 




LEXICON tpt !aŋun:aŋute
+N+Abs+Sg:%CVCTRUNCn	clit ;
+N+Abs+Pl:%>t	clit ;
+N+Rel+Sg:%>m	clit ;
+N+Trm+Sg:%CVCTRUNCn%>mun	clit ;

LEXICON tup !aiviq
+N+Abs+Sg:	clit ;
+N+Abs+Pl:%>%FRICit	clit ;
+N+Trm+Sg:%>mun	clit ;
+N+Abl+Sg:%>miñ	clit ;

LEXICON tup- !seksrik
+N+Abs+Sg:	clit ;
+N+Abs+Pl:%>%TRUNCit	clit ;

LEXICON propmorph
+N:	clit ;


LEXICON pronmorph
+N:	clit ;

LEXICON ObliqueCases_Sg
+Loc+Sg:%>mi	clit ;

LEXICON ObliqueCases_Du
+Loc+Du:%>ni	clit ;

LEXICON ObliqueCases_Pl
+Loc+Pl:%>ni	clit ;


LEXICON verb_intr
+NIAQ:%>niaq	verb_intr ;
+NIAŊIT:%>niaŋit	verb_intr ;
+SAAĠE:%>saaġe	verb_intr_vow ;
+VIK:%>vik	tup- ; 
+V+Ind+1Sg:%>tuŋa	clit ;
+V+Ind+3Sg:%>tuq	clit ;
+V+InfReal+4Sg:%>łuni	clit ;
+V+InfUnreal+4Sg:%>luni	clit ;
+V+Cau+3Sg:%>man	clit ;

LEXICON verb_intr_vow
+NIAQ:%>niaq	verb_intr ;
+NIAŊIT:%>niaŋit	verb_intr ;
+SAAĠE:%>saaġe	verb_intr_vow ;
+VIK:%>vik	tup- ; 
+V+Ind+1Sg:%>tuŋa	clit ;
+V+Ind+3Sg:%>tuq	clit ;
+V+InfReal+4Sg:%>łuni	clit ;
+V+InfUnreal+4Sg:%>luni	clit ;
+V+Cau+3Sg:%>man	clit ;

LEXICON verb_tr
+NIAQ:%>niaq	verb_tr ;
+NIAŊIT:%>niaŋit	verb_tr ;
!+VIK:%>vik	tup- ; !???Can VIK be added to transitives i Iñupiaq???? 
+V+Ind+3Sg+3SgO:%>gaa	clit ;

LEXICON clit
+GUUQ:%>guuq	# ;
+LU:%>lu	# ;
	# ;

