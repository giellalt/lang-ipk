
!====================================
!FST-rules in Inupiaq - Pilot project
!====================================

!Defining the consonant inventory

define stops [ p | P | t | T | c h | k | q ] ;

define cont [ v | V | s | S | s r | l | ł | ḷ | ł2 | y | r | g | k h | ġ | q h  | h ] ; ! ł2 palatalized unvoiced lateral

define nas [ m | M | n | N | ñ | ŋ ] ; 

define cns [ stops | cont | nas ] ;

define vow [ i | e | a | u ] ; ! NB not in accordance with Alaskan tradition: i = strong i whereas e represents the schwa

define dummy [ %> | %TRUNC | %VCTRUNC |%C | %CLIT | %FRIC | %C ] ;

define erfalasoq [ %@P%.IV%.ON%@ | %@P%.TV%.ON%@ | %@R%.IV%.ON%@ | %@R%.TV%.ON%@ | %@D%.IV%.ON%@ | %@D%.TV%.ON%@  ] ;



!********************
!Phonological rules *
!********************

define PartAssKC [ k -> g || _ ( erfalasoq ) %> [ cont | nas ] ] ;
define PartAssQC [ q -> ġ || _ ( erfalasoq ) %> [ cont | nas ] ] ;
define AssKG [ g -> k || k ( erfalasoq) %> %>  _ ] ;
define AssTnas [ t -> n || _ ( erfalasoq ) %> nas ] ;
define AssTJ [ t -> ł || _ %> ł ] ;
define AssTL [ t -> l || _ %> l ] ;
define PartAssTC [ t -> r || _ %> [ v | g ] ] ;
define truncation [ stops -> 0 || _ %TRUNC %> ] ;
define TruncDelete [ %TRUNC -> 0 ] ;
define fricativization [ q -> ġ || _ ( erfalasoq ) %> [ %FRIC | nas ] ] ; !????? Edna does not give the rule, but I suspect it to be so ???????
define AltKN [ k (->) ŋ || _ %> n ] ;
define AltML [ m (->) v || _ %> l ] ;
define AltNL [ n (->) l || _ %> l ] ;
define palatL [ l -> ḷ || i ( cns ) _ ] ;
define palatN [ n -> ñ || i ( cns ) _ ] ;
define palatR [ r -> y || i ( cns ) _ ] ;
define palatT [ t -> c h || i ( cns ) _ ] ;
define Rgemination [ r -> r r || vow _ vow (cns) %C %> ] ;
define Gstop [ g -> k || cns %> _ ] ;
define Qfusion1 [ g -> ġ || q erfalasoq %>  %> _ ] ; !Must clean ipk-lex.txt to avoid two consecutive morphemeborders
define Qfusion2 [ q -> 0 || _ erfalasoq %> %> ġ ] ; !Must clean ipk-lex.txt to avoid two consecutive morphemeborders

!********************
!Household utensils *
!********************

define VeryLongTruncation [ cns vow (cns) -> 0 || _ %CVCTRUNC ] ;
define CleanVeryLongTruncation [ %CVCTRUNC -> 0 ] ;
define DummyDeletion [ dummy -> 0 ] ;

!**********************
!Simple rewrite rules *
!**********************

define SchwaReplace1 [ e -> a || _ ( dummy ) %> [ k | vow ] ] ;
define SchwaReplace2 [ e -> i ] ;


!*************
!The cascade *
!*************

read regex [
truncation
.o.
TruncDelete
.o.
VeryLongTruncation
.o.
CleanVeryLongTruncation
.o.
Rgemination
.o.
fricativization
.o.
SchwaReplace1
.o.
Gstop
.o.
Qfusion1
.o.
Qfusion2
.o.
PartAssQC
.o.
AltKN
.o.
PartAssKC
.o.
PartAssTC
.o.
AssTnas
.o.
AssKG
.o.
AssTL
.o.
AssTJ
.o.
AltML
.o.
AltNL
.o.
DummyDeletion
.o.
palatL
.o.
palatN
.o.
palatR
.o.
palatT
.o.
SchwaReplace2
] ;
